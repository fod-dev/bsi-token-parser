plugins {
    id 'org.jetbrains.dokka' version '1.4.30'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

group 'com.fortify.fod'
version '1.2.0'
description 'Fortify BSI Token Parser library'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter() // Currently required for dokkaJavadoc task, see for example https://github.com/detekt/detekt/issues/3461
}

dependencies {
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile 'com.beust:klaxon:5.5'

    testCompile "org.jetbrains.spek:spek-api:1.1.5"
    testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: '1.4.32'
    testCompile "org.jetbrains.spek:spek-junit-platform-engine:1.1.5"
    testCompile "org.junit.jupiter:junit-jupiter-engine:5.3.2"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:1.4.32"
    testRuntime ('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

test {
    useJUnitPlatform {
        includeEngines 'spek'
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def javadocDir = "$buildDir/libs/javadoc"

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task deleteJavadocFolder(type: Delete) {
    delete javadocDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives(sourcesJar) {
        type 'source'
    }
    archives(javadocJar) {
        type 'javadoc'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                groupId = project.group
                artifactId = project.name
                name = project.name
                version = project.version
                description = project.description
                url = 'https://github.com/fortify/bsi-token-parser-kt'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'fortify'
                        name = 'Fortify'
                        email = 'fortify-oss@fortify.com'
                        organization = 'Micro Focus Fortify'
                        organizationUrl = 'https://www.microfocus.com/en-us/cyberres/application-security'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/fortify/bsi-token-parser-kt.git"
                    developerConnection = "scm:git:https://github.com/fortify/bsi-token-parser-kt.git"
                    url = "https://github.com/fortify/bsi-token-parser-kt"
                }
            }
        }
    }
}

// Sign using signingKey and signingPassword properties
signing {
	def signingKey = findProperty("signingKey")
	def signingPassword = findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	required { gradle.taskGraph.hasTask("publishToOSSRH") }
	sign publishing.publications.maven
}

// Publish using OSSRHUsername and OSSRHPassword properties
nexusPublishing {
	repositories {
		OSSRH {
			nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
			snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
		}
	}
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/BSITokenParser-${version}.pom")
    }
}

task prepareMavenDeployables(type: GradleBuild) {
    tasks = ['clean', 'build', 'test', 'generatePomFileForMavenPublication', 'javadocJar', 'deleteJavadocFolder', 'sourcesJar']
}

defaultTasks 'clean', 'build', 'test'
